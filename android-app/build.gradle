/*
 * Lynket
 *
 * Copyright (C) 2019 Arunkumar
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: "lynket-build"

buildscript {
  ext.kotlin_version = "1.3.72"
  repositories {
    google()
    maven { url "https://jitpack.io" }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:4.1.0-beta05"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "gradle.plugin.dev.arunkumar:scabbard-gradle-plugin:0.4.0"
  }
}

allprojects {
  def repoUrl = "https://raw.github.com/arunkumar9t2/base-android/master/maven-repo"
  def baseAndroid = "dev.arunkumar.android"

  repositories {
    google()
    maven { url "https://jitpack.io" }
    jcenter()
    maven {
      url "${repoUrl}/releases"
      mavenContent {
        releasesOnly()
      }
      content {
        includeGroup baseAndroid
      }
    }
    maven {
      url "${repoUrl}/snapshots"
      mavenContent {
        snapshotsOnly()
      }
      content {
        includeGroup baseAndroid
      }
    }
  }

  configurations {
    all {
      resolutionStrategy {
        force "org.ow2.asm:asm:7.0"
        cacheChangingModulesFor 0, "seconds"
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
