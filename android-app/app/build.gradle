/*
 * Lynket
 *
 * Copyright (C) 2018 Arunkumar
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

// Read from properties
def PLAY_LICENSE_KEY = "PLAY_LICENSE_KEY"
def FABRIC_KEY = "FABRIC_KEY"
def properties = new Properties()
properties.load(new FileInputStream("$rootDir/values.properties"))
def playLicenseKey = properties.getProperty(PLAY_LICENSE_KEY, "\"\"")
def fabricKey = properties.getProperty(FABRIC_KEY, "\"\"")

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        google()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.26.0'
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "arun.com.chromer"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 52
        versionName "2.0.2"

        multiDexEnabled true

        resValue "string", "playLicenseKey", "$playLicenseKey"
        manifestPlaceholders = [crashlyticsApiKey: "$fabricKey"]

        vectorDrawables.useSupportLibrary = true

        // Disable crashlytics by default.
        ext.enableCrashlytics = false
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Lynket"

            lintOptions {
                disable 'MissingTranslation'
            }

            ext.enableCrashlytics = true
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true

            applicationIdSuffix '.debug'
            versionNameSuffix '- dev'
            resValue "string", "app_name", "Lynket-dev"

            ext.enableCrashlytics = false
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-v4:28.0.0'
    }
}


androidExtensions {
    experimental = true
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.8'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation "com.android.support.test:runner:1.0.2"
    testImplementation "com.android.support.test:rules:1.0.2"

    // Debugging
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'

    // Dependency Injection
    implementation 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    kaptTest 'com.google.dagger:dagger-compiler:2.16'

    // Support
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'

    // Architecture Components
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    kapt 'android.arch.lifecycle:compiler:1.1.1'

    // Suggestions
    implementation 'in.arunkumarsampath:suggestions:1.0.3'

    // Intro
    implementation 'com.github.apl-devs:appintro:v4.2.3'

    // Icons
    implementation 'com.mikepenz:community-material-typeface:2.0.46.1@aar'

    // Drawer
    implementation('com.mikepenz:materialdrawer:5.3.6@aar') {
        transitive = true
    }

    // Dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    // Butterknife, Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'

    // Image loading
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    kapt 'com.github.bumptech.glide:compiler:4.4.0'

    // Bottom sheet
    implementation 'com.flipboard:bottomsheet-core:1.5.1'
    implementation 'com.flipboard:bottomsheet-commons:1.5.1'

    // Animations
    implementation 'com.facebook.rebound:rebound:0.3.8'

    // Badge
    implementation 'com.github.nekocode:Badge:1.2'

    // Reactive
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    // Storage
    implementation 'io.paperdb:paperdb:2.6'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true
    }
    implementation 'com.github.chrisbanes:PhotoView:1.3.1'

    implementation "android.arch.paging:runtime:1.0.1"

    // Bottom navigation
    implementation 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.1-rc1'

    // Disk LRU cache
    implementation project(':disk-cache')

    implementation 'com.android.support:multidex:1.0.3'
}